apply plugin: "com.moowork.node"

node {
    version = "8.9.3"
    download = true
    npmVersion = "$npm_version"
}


def npmDeployDir = file("$buildDir/npm")


task copyOutputToNpm(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir
    into npmDeployDir
}


// Note: publish transformed files using dependency on sourceSets.main.output
task preparePublishNpm(dependsOn: [copyOutputToNpm]) {

    if (!project.name.startsWith("d2v"))
        return

    afterEvaluate {
        def d2vDependencies = configurations.compile
                .filter { it.name.contains("d2v") && it.name.contains("-js")}
                .collect { it.name.substring(0, it.name.indexOf("-js") + 3) }

        npmDeployDir.mkdirs()
        def newFile = new File( npmDeployDir, "package.json")
        newFile.write(generatePackageJson(version, project.name, d2vDependencies))
    }

}


task publishNpm(type: NpmTask, dependsOn: [preparePublishNpm]) {

    if (!project.name.startsWith("d2v"))
        return

    if (!project.hasProperty("npmToken")) {
        logger.warn 'warn: to publish on npm the build needs a npmToken property.'
        return
    }


    workingDir = npmDeployDir
    def deployArgs = ['publish',
                      '--access=public',
                      "--//registry.npmjs.org/:_authToken=$npmToken"]
    doFirst {
        if (dryRun == "true") {
            println("$npmDeployDir \$ npm arguments: $deployArgs")
            args = ['pack']
        } else {
            args = deployArgs
        }
    }
}




static def generatePackageJson(String version, String projectName, List dependencies) {

    def d2vDependencies = ""
    if (!dependencies.empty) {
        d2vDependencies = """
  "dependencies": {""" +
                dependencies.collect { """ "@data2viz/$it": "$version" """ }.join(",") + """
  },
"""
    }

return """
{
    "name": "@data2viz/$projectName",
    "version" : "$version",
    "description" : "Data2viz $projectName module",
    "author": "Data2viz",
    "main": "${projectName}.js",
    "license": "Apache-2.0",
    "homepage": "https://data2viz.io",
    $d2vDependencies
    "bugs": {"url": "https://github.com/data2viz/data2viz/issues"},
    "repository": {
        "type": "git",
        "url": "git+https://github.com/data2viz/data2viz.git"
    },
    "keywords": [
        "Kotlin",
        "JavaScript",
        "data2viz"
    ]
}
""".toString()
}

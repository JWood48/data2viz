/**
 * All modules are discovered and added if they respect a convention name. 
 * 
 * First, all d2v-moduleName-common, d2v-moduleName-js,d2v-moduleName-jvm (with the exception of the "data2viz" modules)
 * are discovered and included in the build.
 * 
 * Then, the data2viz modules (d2v-data2viz-common, d2v-data2viz-js and d2v-data2viz-jvm) are included with
 * dependencies on the first list of projects.
 * 
 * Finally, all examples projects are included with dependencies on d2v-data2viz-common, d2v-data2viz-js and
 * d2v-data2viz-jvm. 
 */

includeAll()

def includeAll(){

    def allProjects = []
    //All d2v- projects except the one that holds global dependencies
    rootDir.eachDir {
        if(!it.name.contains("data2viz")){
            allProjects += includeMulti(it.name)
        }
    }

    System.setProperty("all_d2v", allProjects.join(","))
    
    includeMulti("data2viz")
    
    new File(rootDir, "examples").eachDir { includeMulti("examples/" +it.name) }
}

def includeMulti(String path){
    def ret = []
    new File(rootDir, path).eachDir{ 
        def name = includeAndNormalizeName(path + '/' + it.name)
        if ( !name.isEmpty()){
            ret += name
        }
    }
    return ret
}

def includeAndNormalizeName(String path) {
    def name = path.substring(path.lastIndexOf('/')+1)
    if(name.endsWith("-common") || name.endsWith("-jvm") || name.endsWith("-js")) {
        include(name)
        project(":$name").projectDir = file(path)
        return name
    }
    return ""
}
